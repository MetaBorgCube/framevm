module toRISC

imports
	signatures/framevm-sig
	fvm-desugar

rules
	to-RISC-all: FVM_Program(blocks) -> FVM_Program(<map(to-RISC-all)> blocks)
	to-RISC-all: FVM_Block(lbl, FVM_Seq(body)) -> FVM_Block(lbl, FVM_Seq(body_risc))
	  where
		body_risc := <to-RISC(r-dupswap)><to-RISC(r-sget)><to-RISC(r-relative)><to-RISC(r-link)><to-RISC(r-FVM_Cont)> body
  
    // Boilerplate to make the desugaring step simpler
  	to-RISC(s): x -> <foldr(![], (fpr(s) <+ idf))> x 
  	fpr(s): (e, body) -> <concat> [<s>e, body]
  	idf: (e, body) -> [e|body]
  	
  	// Link -> LinkR
	r-link: FVM_Link(path, lbl)  -> [FVM_Get(path),  FVM_LinkR(lbl)]

	// Implicit FVM_Continuations to explicit FVM_Continuations
	r-FVM_Cont: FVM_Return() -> 
		[FVM_Get(FVM_Path([FVM_Cont("c")])), FVM_ContUnpack(), FVM_Swap(), 
		FVM_SetR(FVM_Path([FVM_SlotR()])), FVM_Get(FVM_Path([FVM_Cont("c")])), FVM_ContCall()]
		
	r-FVM_Cont: FVM_Call(FVM_Label(block), FVM_Label(returnAddr)) -> 
		[FVM_Dup(), FVM_ContNew(FVM_Label(returnAddr)), FVM_SetR(FVM_Path([FVM_Cont("c")])), 
		FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("x")])), FVM_SetR(FVM_Path([FVM_Cont("x")])), 
		FVM_ContNewR(FVM_Label(block)), FVM_ContCall()]
	
	r-FVM_Cont: FVM_Call(FVM_Label(block)) -> 
		[FVM_Dup(), FVM_ContUnpack(), FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("x")])), 
		FVM_SetR(FVM_Path([FVM_Cont("x")])), FVM_ContNew(FVM_Label(block)), 
		FVM_SetR(FVM_Path([FVM_Cont("c")])), FVM_ContCall()]
	
	r-FVM_Cont: FVM_ScopeDown(lbl) -> 
		[FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("c")])), FVM_SetR(FVM_Path([FVM_Cont("c")])), 
		FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("x")])), FVM_SetR(FVM_Path([FVM_Cont("x")])), 
		FVM_ContNewR(lbl), FVM_ContCall()]
	
	r-FVM_Cont: FVM_ScopeUp(path, lbl) -> 
		[FVM_Get(path), FVM_ContNewR(lbl), FVM_ContCall()]
		
	r-FVM_Cont: FVM_Try(lbl_try, lbl_catch) ->
		[FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("x")])), FVM_SetR(FVM_Path([FVM_Cont("x")])), 
		FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("c")])), FVM_SetR(FVM_Path([FVM_Cont("c")])), 
		FVM_ContNewR(lbl_catch), FVM_DupN("2"), FVM_Swap(), FVM_SetR(FVM_Path([FVM_Cont("x")])), 
		FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("c")])), FVM_SetR(FVM_Path([FVM_Cont("c")])), 
		FVM_ContNewR(lbl_try), FVM_ContCall()]
		
	r-FVM_Cont: FVM_Try() ->
		[FVM_Dup(), FVM_ContUnpack(), FVM_Dup(), FVM_Get(FVM_Path([FVM_Cont("x")])), 
		FVM_SetR(FVM_Path([FVM_Cont("x")])), FVM_Get(FVM_Path([FVM_Cont("c")])), 
		FVM_SetR(FVM_Path([FVM_Cont("c")])), FVM_DupN("2"), FVM_ContUnpack(), FVM_Swap(), 
		FVM_SetR(FVM_Path([FVM_Cont("x")])), FVM_Dup(), FVM_ContUnpack(), 
		FVM_Get(FVM_Path([FVM_Cont("c")])), FVM_SetR(FVM_Path([FVM_Cont("c")])), FVM_ContCall()]
		
	r-FVM_Cont: FVM_Throw() -> 
		[FVM_Get(FVM_Path([FVM_Cont("x")])), FVM_ContUnpack(), FVM_Swap(), 
		FVM_SetR(FVM_Path([FVM_SlotR()])), FVM_Get(FVM_Path([FVM_Cont("x")])), FVM_ContCall()]
	
	
	
	// FVM_Continuations to relative FVM_Continuations
	r-relative: FVM_ContNew(lbl) -> [FVM_Get(FVM_Self()), FVM_ContNewR(lbl)]
	
	// Get/Set unfolding to single segment paths and only relative gets/sets
	r-relative: FVM_Get() -> [FVM_Get(FVM_Self()), FVM_Swap(), FVM_GetR()]
	r-relative: FVM_Set() -> [FVM_Get(FVM_Self()), FVM_SwapN("2"), FVM_Swap(), FVM_SetR()]
	
	r-relative: FVM_Get(path) -> [FVM_Get(FVM_Self()) | <r-relative> FVM_GetR(path)]
	r-relative: FVM_GetR(FVM_Path(path)) -> <foldr(![], r-relative-get)> path
	
	r-relative: FVM_Set(FVM_Path(path)) -> <concat> [[FVM_Get(FVM_Self()) | <r-relative> FVM_GetR(FVM_Path(body))] , [FVM_Swap(), FVM_SetR(FVM_Path([last]))]]
	  where
	  	(body, last) := <split-init-last> path
	r-relative: FVM_SetR(FVM_Path(path)) -> <concat> [<r-relative> FVM_GetR(FVM_Path(body)) , [FVM_SetR(FVM_Path([last]))]]
	  where
	  	(body, last) := <split-init-last> path
	
	r-relative-get: (e, list) -> [FVM_GetR(FVM_Path([e])) | list]
	
	// Reduce relative gets and sets of slots to getR/0 and setR/0
	// I don't know why you would want to do this, but you can if you really want to
	r-sget: FVM_GetR(FVM_Path([FVM_Slot(slot_id)])) -> [FVM_IPush(slot_id), FVM_GetR()]
	r-sget: FVM_SetR(FVM_Path([FVM_Slot(slot_id)])) -> [FVM_IPush(slot_id), FVM_Swap(), FVM_SetR()]
	
	r-dupswap: FVM_Dup()  -> [FVM_DupN("1")]
	r-dupswap: FVM_Swap() -> [FVM_SwapN("1")]
	