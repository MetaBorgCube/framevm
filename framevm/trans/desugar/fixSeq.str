module fixSeq

imports
    signatures/framevm-sig
    signatures/fvm-stacy-sig
    signatures/fvm-roger-sig
    signatures/fvm-common-sig
    
    fvm-common
    
    analysis/stc-stack
    
rules

    fix-seq: STC_Program(fn, i, s, a, blocks) -> STC_Program(fn, i, s, a, <map(fix-seq)> blocks)
    fix-seq: RGR_Program(fn, i, s, a, blocks) -> RGR_Program(fn, i, s, a, <map(fix-seq)> blocks)
    
    fix-seq: STC_Block(lbl, None(), body, jump)     -> FVM_Block(lbl, FVM_Seq(seq), <stack-calc-stc> seq)
      with
        seq := <concat> [body, [jump]]
        
    fix-seq: STC_Block(lbl, Some(rget), body, jump) -> FVM_Block(lbl, FVM_Seq(seq), <stack-calc-stc> seq)
      with
        seq := <concat> [[rget], body, [jump]]
        
    fix-seq: RGR_Block(lbl, None(), body, jump)     -> FVM_Block(lbl, FVM_Seq(seq), 0)
      with
        seq := <concat> [body, [jump]]
        
    fix-seq: RGR_Block(lbl, Some(rget), body, jump) -> FVM_Block(lbl, FVM_Seq(seq), 0)
      with
        seq := <concat> [[rget], body, [jump]]
    
    
    
    
    resugar-seq: STC_Program(fn, i, s, a, blocks) -> STC_Program(fn, i, s, a, <map(resugar-seq-stc)> blocks)
    resugar-seq: RGR_Program(fn, i, s, a, blocks) -> RGR_Program(fn, i, s, a, <map(resugar-seq-stc)> blocks)
    
    resugar-seq-stc: FVM_Block(lbl, FVM_Seq([h|tail]), s) -> STC_Block(lbl, Some(h), body, jump)
      where
        <is-ret> h;
        (body, jump) := <split-init-last> tail
        
    resugar-seq-stc: FVM_Block(lbl, FVM_Seq(instrs), s) -> STC_Block(lbl, None(), body, jump)
      where
        (body, jump) := <split-init-last> instrs
      
   
    is-ret: r@STC_ContRGetN(_) -> r
    is-ret: r@STC_ContRGet() -> r
