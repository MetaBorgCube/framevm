module stack

imports
	signatures/framevm-sig
	fvm-desugar

rules
	// check stack sizes of blocks
	stack-calc: FVM_Program(blocks) -> FVM_Program(out_blocks)
	  where
    	(errors, out_blocks) := <unzip><mapconcat(stack-calc)> blocks
  
	stack-calc: FVM_PseudoCode(_) -> []
    		
  	stack-calc: FVM_Block(FVM_Label(lbl), FVM_Seq(body)) -> [(errors, FVM_Block(FVM_Label(lbl), FVM_Seq(body), size))]
  	  where
  		(size, errors) := <foldl(stack-update)>(body, (0, []))
  
  	stack-update: (instr, (size, err)) -> (new_size, err)
  	  where
  	  	new_size := <addi> (size, <stack-delta> instr);
  	  	_ := <geq> (new_size, 0)
  	  	
  	 stack-update: (instr, (size, errs)) -> (new_size, [err | errs])
  	  where
  	  	new_size := <addi> (size, <stack-delta> instr);
  	  	err := <debug> ("Popping from empty stack at", instr)
  	  	
	// For each operation, calculate the change in stack size
	stack-delta: FVM_IPush(_) -> 1
	stack-delta: FVM_INeg()   -> 0
	stack-delta: FVM_IAdd()   -> -1
	stack-delta: FVM_IMul()   -> -1
	stack-delta: FVM_ISub()   -> -1
	stack-delta: FVM_IDiv()   -> -1
	stack-delta: FVM_IMod()   -> -1
	
	stack-delta: FVM_IEq()    -> -1
	stack-delta: FVM_ILt()    -> -1
	stack-delta: FVM_IGt()    -> -1
	stack-delta: FVM_IOr()    -> -1
	stack-delta: FVM_IXor()   -> -1
	stack-delta: FVM_IAnd()   -> -1
  
	stack-delta: FVM_New()    -> 1
	stack-delta: FVM_Pop()    -> -1
	stack-delta: FVM_Dup()    -> 1
	stack-delta: FVM_DupN(_)  -> 1
	stack-delta: FVM_Swap()   -> 0
	stack-delta: FVM_SwapN(_) -> 0
  
	stack-delta: FVM_Link(_, _) -> -1
	stack-delta: FVM_LinkR(_)   -> -2
  
	stack-delta: FVM_Get()      -> 0
	stack-delta: FVM_Get(path)  -> 1
	stack-delta: FVM_GetR()     -> -1
	stack-delta: FVM_GetR(path) -> 0
  
	stack-delta: FVM_Set()      -> -2
	stack-delta: FVM_Set(path)  -> -1
	stack-delta: FVM_SetR()     -> -3
	stack-delta: FVM_SetR(path) -> -2
  
	stack-delta: FVM_Print()  -> -1
	stack-delta: FVM_Return() -> -1
  
	stack-delta: FVM_Copy()  -> 1
	stack-delta: FVM_CopyR() -> 0
  
	stack-delta: FVM_JumpZ(lbl1, lbl2) -> -1
	stack-delta: FVM_Jump(lbl) -> 0
	stack-delta: FVM_Call(lbl1, lbl2) -> -1
	stack-delta: FVM_Call(lbl) -> -1
	
	stack-delta: FVM_ScopeUp(path, lbl) -> 0
	stack-delta: FVM_ScopeDown(lbl) -> -1
	
	stack-delta: FVM_ContNew() -> 1
	stack-delta: FVM_ContNew(lbl) -> 1
	stack-delta: FVM_ContNewR(lbl) -> 0
	
	stack-delta: FVM_ContUnpack() -> 0
	stack-delta: FVM_ContCall() -> -1
	
	stack-delta: FVM_Try(_, _) -> -2
	stack-delta: FVM_Try() -> -2
	stack-delta: FVM_Throw() -> -1
	
	stack-delta: FVM_Debug() -> 0
  
  	stack-delta: a -> 0
  	  where
  		_ := <debug> ("Stack calculations not implemented for", a)
