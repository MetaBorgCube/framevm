module util

imports 
    signatures/fvm-common-sig
    interpreter/external
    
    fvm-common
    
rules
    pop-list(|env): 0 -> (env, [])
    pop-list(|env1): n -> (env3, [h|t])
      where
        (env2, h) := <stack-pop-any(|env1)>;
        (env3, t) := <pop-list(|env2)> <dec> n
    
    
    cont-resolve: FVM_Cont(Bind(name, idx))   -> (name, idx)
    cont-resolve: FVM_ContIdx(idx) -> ($[_c[idx]], idx)
      
    fix-string-escaping = string-replace(|"\\n", "\n"); string-replace(|"\\t", "\t"); string-replace(|"\\'", "'"); string-replace(|"\\\"", "\"")
    
         
    // Resolve which label to pick
    jump-eval: (0, then, _) -> then
    jump-eval: (_, _, else) -> else        // Catch all other than 0
    
    
    // Check if the to given values are equal
    eval-eq: (e1, e2) -> IntV(1)
      where <equal> (e1, e2)
    eval-eq: _ -> IntV(0)    
    
    // Check if the to given values are equal
    eval-lt: (e1, e2) -> IntV(1)
      where <lt> (e1, e2)
    eval-lt: _ -> IntV(0)
    
rules
    is-intV: IntV(_) -> IntV(1)
    is-intV: _       -> IntV(0)
    
    is-clos: ClosV(_, _, _) -> IntV(1)
    is-clos: _              -> IntV(0)
    
    is-cont: Continuation(_) -> IntV(1)
    is-cont: _               -> IntV(0)
    
    is-frame: FrameRef(_) -> IntV(1)
    is-frame: _           -> IntV(0)
             
rules    // Util
    // Convert a value to a displayable string 
    val-to-string: IntV(v) -> IntV(v)
    val-to-string: FrameRef(frame) -> frame
    val-to-string: Continuation(cont) -> cont
    val-to-string: ClosV(frame, FVM_BoundLabel(lib, lbl, _), _) -> (frame, lbl)
    