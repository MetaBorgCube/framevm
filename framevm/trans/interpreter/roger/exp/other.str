module other

imports 
    signatures/fvm-roger-sig
    interpreter/external
    interpreter/util
    
    fvm-common

rules
    rgr-eval-exp(|env): RGR_Ref(ref) -> (env, <rgr-get-any(|env)> ref)
    rgr-eval-exp(|env): FVM_BoundLabel(lib, lbl) -> (env, BlockRef(lib, lbl))
    
    rgr-eval-exp(|env): RGR_REq(exp1, exp2) -> (env, v)
      where
        (env2, v1) := <rgr-eval-exp(|env)>  exp1;
        (env3, v2) := <rgr-eval-exp(|env2)> exp2;
        v := <eval-req> (v1, v2)
    
    rgr-eval-exp(|env): RGR_CharLoad(char) -> (env, IntV(char'))
      where
        [char' |_] := <explode-string> <fix-string-escaping> char
        
    rgr-eval-exp(|env): RGR_StringLoad(string) -> (env4, FrameRef(frame))
      where
        string' := <fix-string-escaping> string;
        chars := <explode-string> string';
        size := <length> chars;
        (env2, frame) :=  <frame-new(|env)> <inc> size;
        env3 := <frame-set-slot(|env2)> (frame, 0, IntV(size));
        (env4, _, _) := <foldl(char-store)> (chars, (env3, 1, frame))
    
    char-store: (char, (env1, idx, frame)) -> (env2, <inc> idx, frame)
      where
        env2 := <frame-set-slot(|env1)> (frame, idx, IntV(char))
        
    rgr-eval-exp(|env1): RGR_CFRGet(exp) -> (env3, val)
      where
        (env2, ControlFrame(cf)) := <rgr-eval-exp(|env1)> exp;
        (env3, val) := <cf-receive(|env2)> cf
