module continuation

imports 
    signatures/fvm-roger-sig
    signatures/fvm-common-sig
    interpreter/external
    interpreter/util
    
    fvm-common

rules
    rgr-eval-exp(|env): RGR_CFThis() -> (env, ControlFrame(<cf-this(|env)>))
    rgr-eval-exp(|env): RGR_CFNew(exp) -> (env2, ControlFrame(cf))
      where
        (env1, FrameRef(frame)) := <rgr-eval-exp(|env)> exp;
        (env2, cf) := <vm-cf-new(|env1)> frame
    
    
    rgr-eval-exp(|env): RGR_ContCur(lbl_exp) -> (env2, val)
      where
        (env2, val) := <rgr-eval-exp(|env)> RGR_ContNew(RGR_CFThis(), lbl_exp)
        
    rgr-eval-exp(|env): RGR_ContNew(exp, lbl_exp) -> (env4, Continuation(cont))
      where
        (env2, BlockRef(lib, lbl)) := <rgr-eval-exp(|env)> lbl_exp;
        (env3, ControlFrame(cf)) :=  <rgr-eval-exp(|env2)> exp;
        (env4, cont) := <vm-cont-new(|env3)> (cf, (lib, lbl))
    
    rgr-eval-exp(|env): RGR_ContGet(c) -> (env, Continuation(<cont-get(|env)> (<cf-this(|env)>, <cont-resolve> c)))

    rgr-eval-exp(|env): RGR_ContGet(exp) -> (env2, Continuation(<cont-get(|env)> (<cf-this(|env)>, ($[_c[idx]], idx))))
      where
        (env2, IntV(idx)) := <rgr-eval-exp(|env)> exp
        
    rgr-eval-exp(|env): RGR_ContGet(exp, c) -> (env2, Continuation(<cont-get(|env)> (cf, <cont-resolve> c)))
      where
        <?(FVM_Cont(_)) <+ ?(FVM_ContIdx(_))> c
      where
        (env2, ControlFrame(cf)) := <rgr-eval-exp(|env)> exp

    rgr-eval-exp(|env): RGR_ContGet(exp1, exp2) -> (env3, Continuation(<cont-get(|env)> (cf, ($[_c[idx]], idx))))
      where
        (env2, ControlFrame(cf)) := <rgr-eval-exp(|env)> exp1;
        (env3, IntV(idx)) := <rgr-eval-exp(|env2)> exp2
      
    rgr-eval-exp(|env): RGR_ContUnpack(exp) -> (env2, ControlFrame(frame))
      where
        (env2, Continuation(cont)) := <rgr-eval-exp(|env)> exp;
        frame := <cont-get-cf(|env2)> cont
      
    rgr-eval-exp(|env): RGR_CFUnpack(exp) -> (env2, FrameRef(frame))
      where
        (env2, ControlFrame(cf)) := <rgr-eval-exp(|env)> exp;
        frame := <cf-get-frame(|env2)> cf
        
      
        
        
        