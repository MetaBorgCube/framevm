module framevm

imports Common pseudo

context-free start-symbols Program

sorts Program context-free syntax
  Program.Program = body:Block*	{ layout (align-list body) }
  
  Label.Label = LABEL
  Label.Label = MAIN
  
sorts Block context-free syntax

  Block.Block = <
<lbl:Label>: 
    <body:Body>

> 			{ layout (indent lbl body) }
  
  Block.PseudoCode = <{ 
  	<body:PsdBody>
}

> 			{ layout (indent "{" body && align "{" "}") }
  
  Body.Empty = 
  Body.SeqJump = <<instr:Instr*> 
<jump:ControlInstr>
>			{ layout (align-list instr && align instr jump) }
  
sorts Instr context-free syntax
  
  Instr.IAdd 	 = <addi>					// Consumes two ints from stack and adds them, result is stored on stack
  Instr.IEq 	 = <eqi>					// Consumes two ints from stack and equals them, result is stored on stack
  Instr.ILt 	 = <lti>					// Consumes two ints from stack and performs < on them, result is stored on stack
  Instr.IPush 	 = <ipush <INT>>			// Push int value to stack
  
  Instr.Dup      = <dup>					// Duplicates the value on the stack
  Instr.DupN     = <dup <INTLZ>>			// Duplicates the n-th value on the stack
  Instr.Swap     = <swap>					// Swaps two values on the stack
  Instr.SwapN    = <swap <INTLZ>>			// Swaps the value on top of the stack with the one in (n+1)th position
  Instr.Print    = <print>					// Prints value on the stack
  Instr.Debug    = <debug>					// Prints the state of the vm
  
  
  Instr = FrameInstr
  FrameInstr.New    = <new>					// Create new frame and push pointer on stack
  FrameInstr.Link   = <link <Path> <LABEL>> // Link frame on stack to given location (Empty path is current frame)
  FrameInstr.LinkR	= <linkr <LABEL>>		// Link frame on second position on the stack to the frame on top
  FrameInstr.Set    = <set>					// Store value on top of stack in slot on second position of the current frame
  FrameInstr.Set    = <set <Path>>			// Set value on the stack at path location
  FrameInstr.SetR   = <setr>				// Store value on top of stack in slot on second position of frame on third position
  FrameInstr.SetR   = <setr <Path>>			// Set value on the stack at path location starting in frame on stack
  FrameInstr.Get    = <get>					// Get index on top of stack of the current frame
  FrameInstr.Get    = <get <Path>>			// Store value at path in the stack
  FrameInstr.GetR   = <getr>				// Get index on top of stack in frame on second position
  FrameInstr.GetR   = <getr <Path>>			// Store value at path from frame on stack on stack
  
  
  Instr = ContInstr
  ContInstr.ContNew    = <cnew>  			// Create a new continuation for the current execution point
  ContInstr.ContNew    = <cnew <Label>> 	// Create a continuation of the current frame at a new execution point
  ContInstr.ContNewR   = <cnewr <Label>>  	// Create a continuation for a frame on the stack at a new execution point
  ContInstr.ContUnpack = <cunpack>			// Get the execution frame from a continuation
  
  ControlInstr.Return    = <return>					// Takes return address from opstack and copies top of stack to caller stack  
  ControlInstr.JumpZ     = <jumpz <Label> <Label>>	// Jump to first label if stack zero, else jump to other label
  ControlInstr.Jump      = <jump <Label>>			// Unconditional jump to LABEL
  ControlInstr.Call      = <call <Label> <Label>>	// Call block LABEL, using frame on the stack
  ControlInstr.Call      = <call <Label>>			// Call continuation on the stack with new block
  ControlInstr.ContCall  = <ccall>					// Call a continuation
  
  ControlInstr.Try       = <try <Label> <Label>>	// Execute the frame on second position on the stack and set the first frame as exception handler
  ControlInstr.Try       = <try>					// Execute the continuation on second position on the stack and set the first continuation as exception handler
  ControlInstr.Throw     = <throw>					// Throw the element on top of the stack to the exception handler
  
  ControlInstr.ScopeUp   = <scopeup <Path> <Label>>	// Break execution to a higher scope
  ControlInstr.ScopeDown = <scopedown <Label>>		// Move execution to another scope and copy over the current continuation
  
  Path.Self  = <[]>
  Path.Path  = <[<{PathRef ","}+>]>
  PathRef.Slot = INTLEZ
  PathRef.SlotR = <r>
  PathRef.SlotX = <x>
  PathRef.SlotC = <c>
  PathRef.Link = LABEL

  
/* 

Values 

CodeV
- jump
- return 
- ...

IntV
- add
- pushi
- jumpz

BoolV
- true, false
- and 
- or

FrameRefV
- new
- set
- get 

closure =

Frame with code pointer + reference pointer

*/
  
/*

initialization of frame slots / soundness 

distinction between frame links and reference edges to frames 

well-formedness properties of instructions?

e.g. clean operand stack

*/