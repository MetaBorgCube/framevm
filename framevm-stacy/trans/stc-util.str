module stc-util

imports
	nabl2/api
  
	signatures/framevm-stacy-sig
	signatures/fvm-common-sig
	stc-common

rules
	
	// Convert a flat list of instructions to a VM program
	// Blocks are separated by labels instead of actual blocks
	// Sugar is re-added to make it printable
	stc-from-flat: [n | body] -> <resugar> STC_Program(n, aliasses, blocks)
	  where
	  	<is-string> n;
	  	(aliasses, d_body) := <debug><get-aliasses> body;
		(blocks, []) := <foldr(!([], []), from-flat-fold)> [FVM_Label("MAIN") | d_body]
		
	stc-from-flat: body@[n|_] -> <resugar> STC_Program("0", aliasses, blocks)
	  where
	  	<not(is-string)> n;
	  	(aliasses, d_body) := <get-aliasses> body;
		(blocks, []) := <foldr(!([], []), from-flat-fold)> [FVM_Label("MAIN") | d_body]
    
    get-aliasses: [FVM_LinkAlias(from, to) | body] -> ([FVM_LinkAlias(from, to)|aliasses], body') where (aliasses, body') := <get-aliasses> body
    get-aliasses: [FVM_ContAlias(from, to) | body] -> ([FVM_ContAlias(from, to)|aliasses], body') where (aliasses, body') := <get-aliasses> body
    get-aliasses: body -> ([], body)
    
    
    // Convert the path from an nabl2 lookup to one that can be interpreted by the VM  
    framevm-path-from-nabl2: (name, namespace, property) -> FVM_Path(fvm_path)
      where
    	a            := <nabl2-get-ast-analysis> name;
		ref-occ      := <nabl2-mk-occurrence(|namespace)> name;
		(dec-occ, path) := <nabl2-get-resolved-name(|a)> ref-occ;
		idx          := <nabl2-get-property(|a, property)> dec-occ;
		(path_body, path_end) := <split-init-last> path;
		
//		TODO: Do something with the path
		fvm_path     := <concat>[<map(from-nabl2-path)> path_body, [FVM_Slot(<int-to-string> idx)]]
    
    
    from-nabl2-path: E(scope, lbl) -> FVM_Link(<get-constructor> lbl)
    
    // If the current instruction is a label, close the block (we are going bottom to top)
    // Else just store the instruction and continue
	from-flat-fold: (FVM_Label(lbl), (blocks, instrs)) -> ([STC_Block(FVM_Label(lbl), STC_Seq(instrs)) | blocks], [])
	from-flat-fold: (instr, (blocks, instrs)) -> (blocks, [instr | instrs])
