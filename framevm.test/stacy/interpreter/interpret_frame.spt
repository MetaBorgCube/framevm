module stc_interpreter_frame

language framevm

fixture [[#link Q -> 1
MAIN:
	stack := 99
	[[...]]
	print
	ipush 0
	return
]]

test new [[
	new
]] transform "Run -> Run" to "Frame(frame_1)"


test size 1 [[
	new
	size
]] transform "Run -> Run" to "IntV(0)"

test size 2 [[
	new 2
	size
]] transform "Run -> Run" to "IntV(2)"

test size 3 [[
	ipush 2
	newr
	size
]] transform "Run -> Run" to "IntV(2)"

test size 4 [[
	ipush 2
	size
]] transform "Run -> Run" to "FAIL"

test link 1 [[
	new
	dup
	link [] P
	getr [P]
]] transform "Run -> Run" to "Frame(frame_0)"

test link 2 [[
	new
	dup
	link [] P
	new
	dup
	swap 2
	linkr Q
	dup
	print
	getr [Q, P]
]] transform "Run -> Run" to "Frame(frame_2)
Frame(frame_0)"


test get self [[
	get []
]] transform "Run -> Run" to "Frame(frame_0)"

test get slot 0 [[
	new 1
	newscope P LBL1
LBL1:
	ipush 42
	set [0]
	get [0]
]] transform "Run -> Run" to "IntV(42)"

test get slot multiple [[
	new 2
	newscope P LBL1
LBL1:
	ipush 42
	set [0]
	ipush 13
	set [1]
	get [1]
	print
	get [0]
]] transform "Run -> Run" to "IntV(13)
IntV(42)"


test getr self [[
	new
	getr []
]] transform "Run -> Run" to "Frame(frame_1)"

test getr slot 0 [[
	new 1
	dup
	ipush 42
	setr [0]
	getr [0]
]] transform "Run -> Run" to "IntV(42)"

test getr slot multiple [[
	new 2
	dup
	ipush 42
	setr [0]
	
	dup
	ipush 13
	setr [1]
	
	dup
	getr [1]
	print
	getr [0]
]] transform "Run -> Run" to "IntV(13)
IntV(42)"


test getr slot multiple 2 [[
	new 2
	dup
	ipush 0
	ipush 42
	setr
	
	dup
	ipush 1
	ipush 13
	setr
	
	dup
	ipush 1
	getr
	print
	ipush 0
	getr
]] transform "Run -> Run" to "IntV(13)
IntV(42)"

